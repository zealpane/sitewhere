buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
 	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RELEASE")
		classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
		classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
		classpath 'com.bmuschko:gradle-docker-plugin:3.2.5'
		classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
		classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    	classpath "org.ajoberstar:grgit:2.2.1"
	}
}

ext['docker.base.image'] = 'openjdk:8-jre-alpine'
//ext['docker.base.image'] = 'sitewhere/sitewhere-jdk:9'
ext['docker.maintainer'] = 'SiteWhere "admin@sitewhere.com"'

ext['kafka.version'] = '0.11.0.0'
ext['zookeeper.version'] = '3.4.8'
ext['curator.version'] = '2.12.0'
ext['hazelcast.version'] = '3.9.3'
ext['protobuf.version'] = '3.3.0'
ext['grpc.version'] = '1.12.0'
ext['guava.version'] = '20.0'
ext['netty.version'] = '4.1.14.Final'
ext['groovy.version'] = '2.4.12'
ext['mongodb.version'] = '3.4.2'
ext['solr.version'] = '6.4.0'
ext['siddhi.version'] = '3.1.2'

apply plugin: 'distribution'
    
allprojects {
    group = 'com.sitewhere'
	version = '2.0.rc2'
	
    repositories {
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url 'http://maven.restlet.org' }
        maven { url "https://jitpack.io" }
        maven { url "http://oss.jfrog.org/artifactory/oss-release-local/" }
        maven { url "https://oss.sonatype.org/content/repositories/ksoap2-android-releases/" }
    	maven { url "https://repo.spring.io/libs-milestone" }
        maven { url "http://maven.wso2.org/nexus/content/repositories/releases/" }
    }
}

subprojects {
	apply plugin: 'eclipse'
	
	apply plugin: "com.github.hierynomus.license"
	license {
		header rootProject.file('HEADER')
    	include "**/*.java"
	}
	
    // Use Spring dependency management.
 	apply plugin: 'io.spring.dependency-management'
	dependencyManagement {
		imports {
			mavenBom 'io.spring.platform:platform-bom:Cairo-SR1'
		}
	}

	// Use Docker plugin to allow for creating/pushing an image.
	apply plugin: 'com.bmuschko.docker-remote-api'
	docker {
	    url = dockerProtocol + '://' + dockerHostname + ':' + dockerPort
	    registryCredentials {
	        url = registryUrl
	        username = registryUsername
	        password = registryPassword
	        email = registryEmail
	    }
	}
 	
 	// Choose Java settings.
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
    // Common dependencies used for all projects.
    dependencies {
        testCompile group: 'junit', name: 'junit'
    }
}
